<!DOCTYPE html>
<html>
<head>
  <title>Pipboy</title>
  <link rel="stylesheet" media="all" href="/assets/application-741534c04df171f4cb13b0eb24e16e6e2de3bcad9da6fedc0f0847e491bfa701.css" data-turbolinks-track="true" />
  <script src="/assets/application-375b3bccce10d2f1b55aabf4e3893e710aba22cd9744e3bbec5581066ff0aa40.js" data-turbolinks-track="true"></script>
  <meta name="csrf-param" content="authenticity_token" />
<meta name="csrf-token" content="p0u4DoLoovxh36Ca26aCG4UA3fQSP9HFAW4spsNZwbBqegFfVSv1183VwaN8Q9E2AORioJxE0iaioGW42h25ng==" />
</head>
<body>

<script id='shader-fs' type="x-shader/x-fragment">
  precision mediump float;

  varying vec2 fTexCoord;
  uniform sampler2D uGraphicSampler;
  uniform sampler2D uGlareSampler;
  uniform sampler2D uScanlineSampler;
  uniform sampler2D uDistortSampler;

  uniform float aValue;

  uniform float scroll;
  uniform float offset;

  vec3 composite(vec3 a, vec3 b, vec3 alpha) {
    return a * alpha + b * (vec3(1.0, 1.0, 1.0) - alpha);
  }

  vec3 composite(vec3 a, vec3 b, float alpha) {
    return composite(a, b, vec3(alpha, alpha, alpha));
  }

  vec4 scanLine(vec4 graphic, vec4 glare, vec4 scanline) {
    vec3 backgroundColor = vec3(.0145, .1, .0396) + glare.rgb / 2.0;
    vec3 scanColor = vec3(.145, 1.0, .396);
    vec4 pixelColor = vec4(composite(scanColor, backgroundColor, graphic.a), 1);
    return pixelColor * scanline;
  }

  vec2 verticalOffset(vec2 coords, float offset) {
    float tValue = coords.t;
    tValue += offset;
    if (tValue > 1.0) tValue -= 1.0;
    if (tValue < 0.0) tValue += 1.0;
    return vec2(coords.s, tValue);
  }

  void main(void) {
    // Apply a horizontal wrapping offset to the texture coordinates
    vec2 adjustedTexCoord = verticalOffset(fTexCoord, offset);

    // Sample textures
    vec4 graphic = texture2D(uGraphicSampler, adjustedTexCoord.st);
    vec4 glare = texture2D(uGlareSampler, adjustedTexCoord.st);
    vec4 scanline = texture2D(uScanlineSampler, vec2(adjustedTexCoord.s, adjustedTexCoord.t * 150.0)); 

    // Scanline
    vec4 renderedColor = scanLine(graphic, glare, scanline);

    // Calculate distortion
    float distortHeight = 64.0 * 5.5;   // height in device pixels
    float renderTargetHeight = 1200.0;    // height in device pixels
    vec2 distortSamplePosition = vec2(adjustedTexCoord.s, clamp((adjustedTexCoord.t - (1.0 - scroll)) * (renderTargetHeight / distortHeight), 0.0, 1.0));
    vec4 distort = texture2D(uDistortSampler, distortSamplePosition) * 2.0 * (1.0 - distortSamplePosition.t);
    vec3 distortAlpha = distort.rgb;

    vec3 normalScreen = renderedColor.rgb;
    vec3 screenWithDistortion = normalScreen * 2.5;
    renderedColor = vec4(composite(screenWithDistortion, normalScreen, distortAlpha), 1.0);

    // Calculate rolloff
    float x = adjustedTexCoord.s * 2.0 - 1.0;
    float rolloff = 1.0 - pow(abs(x), 15.0);

    gl_FragColor = renderedColor * rolloff;
  }
</script>

<script id='shader-vs' type="x-shader/x-vertex">
  attribute vec3 aVertexPosition;
  attribute vec2 aTexCoord;

  varying vec2 fTexCoord;

  void main(void) {
    gl_Position = vec4(aVertexPosition, 1.0);

    fTexCoord = aTexCoord;
  }
</script>

<body>
  <!-- Force Monofonto to load -->
  <div style="font-family: Monofonto; font-size: 0px">.</div>

  <canvas id='texture_canvas' width='800' height='600' hidden='true'></canvas>

  <div id='pipboy_canvas_container'>
    <canvas id='pipboy_canvas' width='1600' height='1200' style='width: 800px; height: 600px'></canvas>
  </div>
</body>


</body>
</html>
